apply plugin: 'com.android.application'

// See stackoverflow.com/questions/28498688
def makeGitDescription = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.acmetensortoys.ctfwstimer"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 14
        versionName "1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "gitDescription", "\"${makeGitDescription()}\""
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Cam: testing on hardware, whee
            debuggable true
            buildConfigField "String", "gitDescription", "\"${makeGitDescription()}\""
        }
    }

    flavorDimensions "play"
    productFlavors {
        noplay {
            dimension "play"
        }
        play {
            dimension "play"
        }
    }

    // TODO: Should filter out play+debug since it will overflow dex method
    // limits (yikes)
}

repositories {
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-releases/"
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    api project(":lib")

    api 'com.android.support:appcompat-v7:28.0.0'
    api 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'

    // Nah.
    // wearApp project(':wear')
    //// Needed for wear data synchronization; blech
    // playApi 'com.google.android.gms:play-services-wearable:16.0.1'

    api 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.0'
    api('org.eclipse.paho:org.eclipse.paho.android.service:1.1.1') {
        exclude module: 'support-v4'
    }

    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}
